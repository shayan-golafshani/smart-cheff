apply plugin: 'com.android.application'
apply plugin: "androidx.navigation.safeargs"


ext {
    appName = 'SmarrtCheff'
}


static def getProperty(filename, property) {
    Properties properties = new Properties()
    properties.load(new FileInputStream(filename))
    return properties.getProperty(property)
}
static def quoted (String value){
    return "\"$value\""
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "edu.cnm.deepdive.smartcheff"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"


        def connection_properties = "$projectDir/../../../services/smartcheff.properties"
        buildConfigField "String", "CLIENT_ID",
                quoted(getProperty(connection_properties, "client_id"))
        buildConfigField "String", "BASE_URL",
                quoted(getProperty(connection_properties, "base_url"))
        buildConfigField "String", "API_KEY",
                quoted(getProperty(connection_properties, "api_key"))


        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ['room.schemaLocation': "$projectDir/schemas".toString()]
            }
        }

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8

    }
    buildFeatures{
        viewBinding true
    }

}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'

    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    implementation 'androidx.navigation:navigation-fragment:2.3.1'
    implementation 'androidx.navigation:navigation-ui:2.3.1'



    implementation 'io.reactivex.rxjava2:rxjava:2.2.20'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    def room_version = '2.2.5'
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava2:$room_version"

    implementation 'com.facebook.stetho:stetho:1.5.1'

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    def lifecycle_version = '2.2.0'
    implementation "androidx.lifecycle:lifecycle-runtime:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    // Gson (Json parser) dependency
    implementation 'com.google.code.gson:gson:2.8.6'

    // Retrofit dependency (REST client) with ReactiveX & Gson integration
    def retrofit_version = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    //OkHttp logging dependency
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'

    //Google sign In
    implementation 'com.google.android.gms:play-services-auth:19.0.0'

    // Preferences/settings dependencies
    implementation 'androidx.preference:preference:1.1.1'

    implementation 'com.squareup.picasso:picasso:2.71828'
}

android.applicationVariants.all { variant ->
    task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
        def docTitle = "${appName} ${project.android.defaultConfig.versionName}"
        def compileTask = variant.javaCompileProvider.get()
        def androidLibJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
        title docTitle
        description "Generates Javadoc for $variant.name."
        source = compileTask.source
        destinationDir = file("${projectDir}/../docs/api")
        doFirst {
            classpath = files(compileTask.classpath.files) + files(androidLibJar)
        }
        options {
            links 'https://docs.oracle.com/javase/8/docs/api/'
            links 'http://reactivex.io/RxJava/javadoc/'
            links 'https://javadoc.io/doc/io.reactivex.rxjava2/rxandroid/latest/'
            links 'https://javadoc.io/doc/com.google.code.gson/gson/latest/'
            links 'https://square.github.io/retrofit/2.x/retrofit/'
            links 'https://square.github.io/okhttp/3.x/okhttp/'
            links 'https://square.github.io/okhttp/3.x/logging-interceptor/'
            links 'https://square.github.io/picasso/2.x/picasso/'
            links 'https://javadoc.io/doc/com.facebook.stetho/stetho/latest/'
            links 'https://commons.apache.org/proper/commons-csv/apidocs/'
            linksOffline 'https://developer.android.com/reference', "${projectDir}/../link-offline/android"
            linksOffline 'https://developers.google.com/android/reference', "${projectDir}/../link-offline/google"
            windowTitle docTitle
        }
    }
}